plugins {
	id 'org.springframework.boot' version '2.5.5'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'jacoco'
	id 'com.github.kt3k.coveralls' version '2.12.0'
	id "com.heroku.sdk.heroku-gradle" version "2.0.0"
}

group = 'com.travisci.springboot.matjawor'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
	dependsOn test // tests are required to run before generating the report
	reports {
		xml.enabled = true // coveralls plugin depends on xml format report
		html.enabled = true
	}
}

coveralls {
	jacocoReportPath 'build/reports/jacoco/test/jacocoTestReport.xml'
}

heroku {
	appName = "mjApp"
}

heroku {
	includes = ["build/libs/app-0.0.1-SNAPSHOT.jar"]
	includeBuildDir = false
	jdkVersion = 11
	processTypes(
			web: "java -jar build/libs/app-0.0.1-SNAPSHOT.jar"
	)
}

// on root context with -PsubprojectName parameter
tasks.register('getSubprojectVersion') {
	doLast {
		subprojects.find {if (it.name == subprojectName) {
			println (it.version)
		} }
	}
}

// on subproject context with :subprojectName:taskName
subprojects {
	tasks.register('getProjectVersion') {
		doLast {
			println(project.version)
		}
	}
}


